; TRAFFIC LIGHT CONTROLLER USING 3 LED AND 7 SEGMENT TO DISPLAY THE TIME FOR EACH LED WHEN IT IS ON
; LED USED(GREEN, RED, YELLOW)
; USING THE MCLR ALLOWS TO RESET THE MICROCONTROLLER
; PIC18F452 MICROCONTROLLER USED
; THE SIMULATION WORKS VERY WELL AND IT WAS TESTED OVER AND OVER
; FOR RED LIGHT(COUNTS FROM 00 TO 59) AND THEN GREEN LIGHT(COUNTS FROM 00 TO 30) AND THEN YELLOW LIGHT(FROM 00 TO 5)
; AUTHOR : DALI FETHI ABDELLATIF, 07/03/2024
			CONFIG		WDT = OFF			; WATCHDOG IS OFF
			LIST		P = 18F452
			#INCLUDE	"p18f452.inc"		; HEADER FILE THAT CONTAINS ALL THE DEFINITIONS OF THE REGISTERS AND BITS
;-------------------- EQUAT VALUES ----------------------------------
STATE		EQU			0x00
COUNTER		EQU			0x01
DISPLAY		EQU			0x02
;---------------- THE BEGINING --------------------------------------
			ORG			0x00000
			BRA			MAIN
			ORG			0x00008			; INTERRUPT VECTOR ADDRESS
			BRA			INTERRUPT
;--------------------------------------------------------------------
MAIN
			CLRF		TRISC
			CLRF		PORTC
			CLRF		COUNTER
			MOVLW		0xF1	
			MOVWF		TRISB
			BCF			PORTB,RB1
			BCF			PORTB,RB2
			BCF			PORTB,RB3
			BCF			TRISE,0
			BCF			TRISE,1
			SETF		PORTE	
			BCF			STATE,0
			CLRF		DISPLAY
;			CONFIGURING INTERRUPT FOR (TIMER0, TIMER1)
			BCF			INTCON2,RBPU
			BSF			INTCON,GIE
			BSF			INTCON,PEIE
			BSF			INTCON,TMR0IE
			BCF			INTCON,TMR0IF
			BSF			PIE1,TMR1IE
			BCF			PIR1,TMR1IF
;			CONFIGURING THE TIMERS
			MOVLW		0x15
			MOVWF		T0CON
			MOVLW		0xC2
			MOVWF		TMR0H
			MOVLW		0xF7
			MOVWF		TMR0L
			BSF			T0CON,TMR0ON
			MOVLW		0x84		
			MOVWF		T1CON
			MOVLW		0x3C
			MOVWF		TMR1H
			MOVLW		0xB0
			MOVWF		TMR1L
			BSF			T1CON,TMR1ON
;------------------------------------------------------------------
WAIT
			NOP
			BRA		WAIT
;----------------- INTERRUPT VECTOR TABLE --------------------------
INTERRUPT
			BTFSC		INTCON,TMR0IF	; TESTING THE INTERRUPT FLAG IF IT IS RAISED
			BRA			TEST
			BTFSC		PIR1,TMR1IF		; TESTING THE INTERRUPT FLAG IF IT IS RAISED
			BRA			TESTING
TESTING
			BTFSC		STATE,0			; VARIABLE TO CONTROL WHICH DIGIT WILL BE DISPLAYED
			BRA			SECOND
			BRA			ONES
TEST
			MOVF		DISPLAY,W		; VARIABLA TO CONTROL THE TIME FOR EACH LED
			XORLW		0x00
			BTFSC		STATUS,Z
			BRA			RED
			MOVF		DISPLAY,W
			XORLW		0x01
			BTFSC		STATUS,Z
			BRA			GREEN
			BRA			YELLOW		
;------------------INTERRUPT SERVICE ROUTING FOR THE FIRST DIGIT OF THE 7 SEGMENT-------------------------
ONES
			BSF			STATE,0
			BCF			PIR1,TMR1IF
			MOVLW		0x3C
			MOVWF		TMR1H
			MOVLW		0xB0
			MOVWF		TMR1L
			MOVF		COUNTER,W
			ANDLW		0x0F	
			XORLW		0x0A
			BTFSC		STATUS,Z
			RCALL		HERE
			MOVLW		0x01	
			MOVWF		PORTE
			MOVF		COUNTER,W	
			ANDLW		0x0F
			MOVWF		PORTC
			MOVF		DISPLAY,W
			XORLW		0x00
			BNZ			HEREE
			BSF			PORTB,RB1
			BCF			PORTB,RB2
			BCF			PORTB,RB3
			RETFIE
HERE
			MOVF		COUNTER,W	
			ADDLW		0x06
			MOVWF		COUNTER
			RETURN	
HEREE
			MOVF		DISPLAY,W
			XORLW		0x01
			BTFSC		STATUS,Z
			BRA			ALLOW
			BCF			PORTB,RB3
			BCF			PORTB,RB1
			BSF			PORTB,RB2
			BRA			FINALE
ALLOW
			BSF			PORTB,RB3
			BCF			PORTB,RB1
			BCF			PORTB,RB2
FINALE
			RETFIE
;-----------INTERRUPT SERVICE ROUTING FOR THE SECOND DIGIT OF THE 7 SEGMENT--------------------------------
SECOND
			BCF			PIR1,TMR1IF
			BCF			STATE,0
			MOVLW		0x02	
			MOVWF		PORTE
			SWAPF		COUNTER,W
			ANDLW		0x0F
			MOVWF		PORTC
			MOVLW		0x3C
			MOVWF		TMR1H
			MOVLW		0xB0
			MOVWF		TMR1L
			RETFIE	
;-------------INTERRUPT SERVICE ROUTING FOR INCREMENTING THE VALUE OF THE COUNTER IF THE RED LIGTH IS ON------------------------------
RED
			BCF			INTCON,TMR0IF
			MOVLW		0xC2
			MOVWF		TMR0H
			MOVLW		0xF7
			MOVWF		TMR0L
			MOVLW		0x01
			ADDWF		COUNTER,F
			MOVF		COUNTER,W
			XORLW		0x5A	
			BTFSC		STATUS,Z
			BRA			CHANGE
			RETFIE
CHANGE
			MOVLW		0x01
			MOVWF		DISPLAY
			CLRF		COUNTER
			RETFIE		
;-----------------INTERRUPT SERVICE ROUTING FOR INCREMENTING THE VALUE OF THE COUNTER IF THE YELLOW LIGTH IS ON-----------------------------
YELLOW
			BCF			INTCON,TMR0IF
			MOVLW		0xC2
			MOVWF		TMR0H
			MOVLW		0xF7
			MOVWF		TMR0L
			MOVLW		0x01
			ADDWF		COUNTER,F
			MOVF		COUNTER,W
			XORLW		0x06
			BTFSC		STATUS,Z
			BRA			CHANGEE
			RETFIE
CHANGEE
			MOVLW		0x00
			MOVWF		DISPLAY
			CLRF		COUNTER
			RETFIE
;-----------------INTERRUPT SERVICE ROUTING FOR INCREMENTING THE VALUE OF THE COUNTER IF THE GREEN LIGTH IS ON-----------------------------
GREEN
			BCF			INTCON,TMR0IF
			MOVLW		0xC2
			MOVWF		TMR0H
			MOVLW		0xF7
			MOVWF		TMR0L
			MOVLW		0x01
			ADDWF		COUNTER,F
			MOVF		COUNTER,W
			XORLW		0x31	
			BTFSC		STATUS,Z
			BRA			CHANGEEE
			RETFIE
CHANGEEE
			MOVLW		0x02
			MOVWF		DISPLAY
			CLRF		COUNTER
			RETFIE
;--------------------------------------------------------------------------------------------------------------------------------------------------
			END
			
